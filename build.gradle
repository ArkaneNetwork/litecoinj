import org.gradle.util.GradleVersion

buildscript {
    repositories {
        mavenCentral()
    }
    boolean isGradle7 = GradleVersion.current().compareTo(GradleVersion.version("7.0")) >= 0
    def gradleProtobufVersion = isGradle7 ? "0.8.18" : "0.8.10"
    if (isGradle7) {
        System.err.println "Warning: Using com.google.protobuf:protobuf-gradle-plugin:${gradleProtobufVersion} because ${GradleVersion.current()}"
    }
    dependencies {
        classpath "com.google.protobuf:protobuf-gradle-plugin:${gradleProtobufVersion}"
    }
}

plugins {
    id 'maven-publish'
    id 'java'
}

group = 'org.litecoinj'
version = '1.17.0' // Replace with your project's version

publishing {
    publications {
        litcoinj(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = 'nexus'
            def releasesRepoUrl = 'https://pkg.arkane.network/repository/maven-releases/'
            def snapshotsRepoUrl = 'https://pkg.arkane.network/repository/maven-snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = findProperty('nexusUsername') ?: System.getenv('NEXUS_USERNAME')
                password = findProperty('nexusPassword') ?: System.getenv('NEXUS_PASSWORD')
            }
        }
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
}
